<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Placing" Id="{ca35c11c-5418-4c8a-a17d-42988f29283b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Placing
VAR_INPUT
	Inp_Enable				: BOOL;
	Inp_Reset				: BOOL;
	Inp_SensorBack			: BOOL;
	Inp_SensorMiddle		: BOOL;
	Inp_SensorFront			: BOOL;
	Cmd_Flushing			: BOOL;
	bRobotMove				: BOOL;
	Inp_Done_Place			: BOOL;
	Inp_Req					: BOOL;
	i_Data					: UINT;
	i_Pattern				: UINT;
	i_MaxStacking			: UINT;
	Inp_FB					: BOOL;
	Set_Tmr_Check			: TIME;
	Inp_GlobalReset			: BOOL;
	Inp_ResetData			: BOOL;
	bBypassFGHS				: BOOL;
	CreateID				: STRING;
	
END_VAR
VAR_OUTPUT
	Out_Motor_Fwd			: BOOL;
	Out_Motor_Rev			: BOOL;
	Out_Req					: BOOL;
	Out_FB					: BOOL;
	Out_PalletJammed		: BOOL;
	Out_Rob_Placing			: BOOL;
	CurrentJob				: STRING;
	SeqIndex				: INT;
	PalletOut				: BOOL;
END_VAR
VAR_OUTPUT PERSISTENT
	PalletID				: STRING;
	CounterBarcode			: BYTE;
END_VAR

VAR
	Sts_Flushing			: BOOL;
	o_MaxCycle				: UINT;
	DataTimer				: TON;
	TWatchDog				: TON;
	Sequence				: INT;
	
END_VAR

VAR_IN_OUT
	AccFlushing				: BYTE;
	Data_Count_Place		: USINT;
	Data_Count_Real			: USINT;
	Data_Incomplete			: UINT;
	Data_Pallet_Total		: UINT;
	Data_Count_Total		: UINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
IF Inp_Reset AND Out_PalletJammed THEN
	Out_PalletJammed	:= FALSE; // Reset sinyal jammed jika ada reset masukan dan palet terjammed
	Sequence 			:= 0; // Kembalikan urutan langkah ke awal
END_IF

IF Inp_Enable THEN // Jika masukan diaktifkan
	
	IF Cmd_Flushing THEN // Jika ada perintah untuk flushing
		Sts_Flushing:=TRUE; // Set status flushing menjadi TRUE
	END_IF;
	
    CASE Sequence OF
        0:	(* Check Motor *)
			 Out_Motor_Fwd := TRUE; // Nyalakan motor maju
			 DataTimer(IN:=TRUE, PT:=T#4S); // Mulai timer data dengan durasi 4 detik
			 IF DataTimer.Q THEN // Jika timer data berakhir
				 DataTimer(IN:=FALSE); // Matikan timer data
				 Out_Motor_Fwd	:= FALSE; // Matikan motor maju
				 Sequence		:= 10; // Pergi ke langkah 10
			 END_IF

		10:  (* Check Sensor *)
			CurrentJob		:= 'Check Sensor'; // Tentukan pekerjaan saat ini
			IF (Inp_SensorFront OR Inp_SensorMiddle OR Inp_SensorBack) AND AccFlushing = 2 THEN // Jika salah satu sensor terdeteksi dan proses flushing selesai
				Sequence := 600; // Pergi ke langkah 600
			ELSIF (Inp_SensorFront OR Inp_SensorMiddle OR Inp_SensorBack) AND ((Data_Count_Place > o_MaxCycle AND o_MaxCycle <> 0)  OR Sts_Flushing) THEN
                Sequence := 500; // Pergi ke langkah 500 jika syarat terpenuhi
            ELSIF Inp_SensorFront THEN
                Sequence := 300; // Pergi ke langkah 300 jika hanya sensor depan yang terdeteksi
            ELSIF NOT Inp_SensorFront AND (Inp_SensorMiddle OR Inp_SensorBack)  THEN
                Sequence := 200; // Pergi ke langkah 200 jika hanya sensor tengah atau belakang yang terdeteksi
            ELSE
                Sequence := 100; // Pergi ke langkah 100 jika tidak ada sensor yang terdeteksi
            END_IF;
			
			
        100:    (* Request *)
			CurrentJob		:= 'Request Pallet'; // Tentukan pekerjaan saat ini
			Sts_Flushing	:= FALSE; // Reset status flushing
            Out_Req			:= TRUE; // Kirim permintaan pallet
			Out_FB			:= FALSE; // Matikan sinyal kembali
            IF Inp_FB THEN // Jika ada balasan dari sistem lain
				DataTimer(IN:=FALSE); // Matikan timer data
                Sequence := 200; // Pergi ke langkah 200
            END_IF;       
			
        200:    (* Pallet Masuk *)
			CurrentJob	:= 'Pallet In'; // Tentukan pekerjaan saat ini
			PalletOut	:= FALSE; // Reset status keluarnya palet
			IF NOT Inp_SensorFront AND NOT Inp_SensorMiddle AND NOT Inp_SensorBack THEN // Jika tidak ada sensor yang terdeteksi
            	Out_Req := TRUE; // Kirim permintaan pallet
			END_IF
            Out_Motor_Fwd := TRUE; // Nyalakan motor maju
            IF Inp_SensorFront AND (Inp_SensorBack OR Inp_SensorMiddle)  THEN // Jika hanya sensor depan yang terdeteksi, tetapi sensor belakang atau tengah juga terdeteksi
				DataTimer(IN := TRUE,PT := Set_Tmr_Check); // Mulai timer data dengan durasi yang telah ditentukan
                IF DataTimer.Q THEN // Jika timer data berakhir
					TWatchDog(IN := FALSE); // Matikan watchdog
                    DataTimer(IN:=FALSE); // Matikan timer data
					Out_PalletJammed	:= FALSE; // Reset sinyal palet terjammed
					Out_Motor_Fwd		:= FALSE; // Matikan motor maju
           			Out_Req				:= FALSE; // Matikan permintaan palet
                    Sequence			:= 300; // Pergi ke langkah 300
                END_IF;    
			ELSE
				TWatchDog(IN := TRUE, PT := T#30S); // Mulai watchdog dengan durasi 30 detik
				IF TWatchDog.Q THEN // Jika watchdog berakhir
					Sequence	:= 9999; // Pergi ke langkah akhir
				END_IF;
            END_IF;
			
        300:
			CurrentJob	:= 'Check Cycle'; // Tentukan pekerjaan saat ini
			IF o_MaxCycle <> 0 THEN // Jika siklus maksimum tidak nol
				IF o_MaxCycle >= Data_Count_Place  THEN // Jika siklus maksimum kurang dari atau sama dengan data tempat
					Sequence := 400; // Pergi ke langkah 400
				ELSE
					DataTimer

(IN := TRUE,PT := T#2S); // Mulai timer data dengan durasi 2 detik
					IF DataTimer.Q THEN // Jika timer data berakhir
						DataTimer(IN:=FALSE); // Matikan timer data
						Sequence := 500; // Pergi ke langkah 500
					END_IF;
				END_IF;
			END_IF
			
        400:
			CurrentJob		:= 'Place Ready'; // Tentukan pekerjaan saat ini
			AccFlushing		:= 0; // Reset akumulator flushing
            Out_Rob_Placing := TRUE; // Aktifkan robot untuk meletakkan palet
			PalletID		:= CreateID; // Buat ID palet baru
            IF Inp_Done_Place THEN // Jika proses penempatan selesai
                    Data_Count_Real		:= Data_Count_Real + 1; // Tambahkan data tempat yang sebenarnya
                    Data_Count_Place	:= Data_Count_Place + 1; // Tambahkan data tempat
                    Sequence 			:= 450; // Pergi ke langkah 450
            ELSIF (Cmd_Flushing OR Sts_Flushing) AND NOT bRobotMove THEN // Jika ada perintah flushing atau status flushing aktif dan robot tidak bergerak
                Data_Incomplete			:= Data_Incomplete + 1; // Tambahkan data yang tidak lengkap
                Sts_Flushing			:= TRUE; // Set status flushing menjadi TRUE
                Sequence 				:= 500; // Pergi ke langkah 500				
            END_IF;
			
        450:
            Out_Rob_Placing := FALSE; // Matikan robot untuk meletakkan palet
            IF NOT Inp_Done_Place THEN // Jika proses penempatan belum selesai
                Sequence := 460; // Pergi ke langkah 460
            END_IF;	
			
		460:
			IF PalletID <> '' THEN // Jika ID palet tidak kosong
				CounterBarcode	:= Data_Count_Real; // Tentukan nomor barcode
				Sequence 		:= 300; // Pergi ke langkah 300
			END_IF
			
		500:
			
			CurrentJob		:= 'Create Pallet ID'; // Tentukan pekerjaan saat ini
			DataTimer(IN := TRUE,PT := T#500MS); // Mulai timer data dengan durasi 500 milidetik
			IF DataTimer.Q THEN // Jika timer data berakhir
				DataTimer(IN := FALSE); // Matikan timer data
				Sequence		:= 550; // Pergi ke langkah 550
			END_IF
						
		550:
			AccFlushing		:= 1; // Set akumulator flushing menjadi 1
			Sequence		:= 600; // Pergi ke langkah 600
			
		600:
			Out_Rob_Placing := FALSE; // Matikan robot untuk meletakkan palet
			Out_FB 			:= FALSE; // Matikan sinyal kembali
			Out_Req 		:= FALSE; // Matikan permintaan palet
			CurrentJob		:= 'Waiting Acc Flushing'; // Tentukan pekerjaan saat ini
			IF (PalletID <> '' AND AccFlushing = 2) OR bBypassFGHS OR Data_Count_Real = 0 THEN // Jika ID palet tidak kosong dan flushing selesai atau bypass FGHS atau data tempat tidak ada
                Sequence := 1000; // Pergi ke langkah 1000
            END_IF;
		
            
        1000:    (* Menunggu Request *)
			Out_Rob_Placing := FALSE; // Matikan robot untuk meletakkan palet
			CurrentJob		:= 'Waiting Request'; // Tentukan pekerjaan saat ini
			Out_FB 			:= TRUE; // Aktifkan sinyal kembali
			Out_Req 		:= TRUE; // Kirim permintaan palet
            IF Inp_Req AND Inp_FB AND (AccFlushing = 2 OR bBypassFGHS OR Data_Count_Real = 0)  THEN // Jika permintaan diterima dan sinyal kembali aktif dan flushing selesai atau bypass FGHS atau data tempat tidak ada
                Sequence := 1100; // Pergi ke langkah 1100
            END_IF;
			
        1100:    (* Pallet Keluar *)
			CurrentJob		:= 'Pallet Out'; // Tentukan pekerjaan saat ini
            Out_Motor_Rev	:= TRUE; // Nyalakan motor mundur
            IF (NOT Inp_SensorBack AND NOT Inp_SensorFront )AND NOT Inp_Req THEN // Jika tidak ada sensor belakang dan depan terdeteksi dan tidak ada permintaan
                DataTimer(IN := TRUE,PT := T#500ms); // Mulai timer data dengan durasi 500 milidetik
                IF DataTimer.Q THEN // Jika timer data berakhir
					TWatchDog(IN := FALSE); // Matikan watchdog
					DataTimer(IN:=FALSE); // Matikan timer data
					Out_FB			:= FALSE; // Matikan sinyal kembali
					Out_Motor_Rev	:= FALSE; // Matikan motor mundur
                    Sequence		:= 1200; // Pergi ke langkah 1200
                END_IF;
			ELSE
				TWatchDog(IN := TRUE, PT := T#15S); // Mulai watchdog dengan durasi 15 detik
				IF TWatchDog.Q THEN // Jika watchdog berakhir
					Sequence		:= 9999; // Pergi ke langkah akhir
				END_IF;
            END_IF;
			
        1200:
			AccFlushing			:= 0; // Reset akumulator flushing
			IF NOT Sts_Flushing THEN // Jika status flushing tidak aktif
			Data_Pallet_Total	:= Data_Pallet_Total + 1; // Tambahkan jumlah palet total
			END_IF
			Data_Count_Total	:= Data_Count_Total + Data_Count_Real; // Tambahkan jumlah data tempat sebenarnya
			Sequence			:= 1300; // Pergi ke langkah 1300
		   
        1300:
			CounterBarcode		:= 0; // Reset nomor barcode
            Data_Count_Real		:= 0; // Reset jumlah data tempat sebenarnya
			Data_Count_Place	:= 1

; // Set jumlah data tempat menjadi 1
			PalletID			:= ''; // Reset ID palet
			PalletOut			:= TRUE; // Set status keluarnya palet
            IF NOT Inp_Req THEN // Jika tidak ada permintaan
                Sequence		:= 100; // Kembali ke langkah 100
            END_IF;
			
		9999:
			TWatchDog(IN := FALSE); // Matikan watchdog
			DataTimer(IN:=FALSE); // Matikan timer data
			Out_PalletJammed	:= TRUE; // Aktifkan sinyal palet terjammed
			Out_Motor_Rev 		:= FALSE; // Matikan motor mundur
			Out_Motor_Fwd 		:= FALSE; // Matikan motor maju
			
        END_CASE;
ELSE
  	DataTimer(IN:=FALSE); // Matikan timer data
	TWatchDog(IN := FALSE); // Matikan watchdog
	Out_Motor_Rev 		:= FALSE; // Matikan motor mundur
	Out_Motor_Fwd 		:= FALSE; // Matikan motor maju
    Out_Rob_Placing 	:= FALSE; // Matikan robot untuk meletakkan palet
    Out_Req 			:= FALSE; // Matikan permintaan palet
	Out_FB 				:= FALSE; // Matikan sinyal kembali
	CurrentJob			:= 'Machine Stopped'; // Tentukan pekerjaan saat mesin berhenti
	IF Inp_GlobalReset THEN // Jika reset global diberikan
        Sequence 		:= 0; // Kembalikan urutan langkah ke awal
        Out_FB 			:= FALSE; // Matikan sinyal kembali
        Sts_Flushing 	:= FALSE; // Matikan status flushing
    END_IF;
END_IF;

//Sequence Index
SeqIndex	:= Sequence; // Tentukan urutan langkah

IF Data_Count_Place = 0 THEN // Jika jumlah data tempat kosong
    Data_Count_Place  := 1; // Set jumlah data tempat menjadi 1
END_IF;

Data_Count_Place := Data_Count_Real+1; // Set jumlah data tempat menjadi jumlah data tempat sebenarnya ditambah 1

// Calculate Max Cycle
o_MaxCycle :=i_Data*i_MaxStacking; // Hitung siklus maksimum

IF Inp_ResetData THEN // Jika reset data diberikan
    Data_Count_Total	:= 0; // Reset jumlah data total
    Data_Pallet_Total 	:= 0; // Reset jumlah palet total
    Data_Incomplete 	:= 0; // Reset jumlah data yang tidak lengkap
END_IF;
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>